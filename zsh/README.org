#+TITLE: Zsh Configuration
#+PROPERTY: header-args :tangle-mode (identity #o444) :comments both
#+AUTO_TANGLE: t

This is my Zsh configuration, which supplanted Bash for me several years ago now. For what goes where for Zsh, see [[https://zsh.sourceforge.io/Guide/zshguide02.html][this guide page]] and [[https://zsh.sourceforge.io/Doc/Release/Files.html][this doc page]].

* .zshenv
:PROPERTIES:
:header-args+: :tangle ".zshenv"
:END:
Set up a few environment variables we need everywhere, especially the One True Editor (with a fallback of nano)
#+begin_src sh
  export DEFAULT_USER=$USER
  #TERM='xterm-256color'

  export EDITOR='emacsclient -a nano'
#+end_src

workaround core-updates-frozen bug with TZ and HOSTNAME not universal
#+begin_src sh
  export TZ=America/New_York
  export HOSTNAME=$(hostname)
#+end_src

ssh with pgp
#+begin_src sh
  export GPG_TTY="$(tty)"
  export SSH_AUTH_SOCK=$(gpgconf --list-dirs agent-ssh-socket)
  gpgconf --launch gpg-agent
#+end_src

Add a user local bin to ~PATH~ (right now just for XMonad with Stack, default install location)
#+begin_src sh
  #typeset -U PATH path
  #PATH=("$HOME/.local/bin" "$path[@]")
  PATH=$HOME/.local/bin:$PATH
  export PATH
#+end_src

Get colors from [[https://github.com/deviantfero/wpgtk/][wpgtk]] so commands in non-interactive shells also have them
#+begin_src sh :tangle no
  (cat ~/.config/wpg/sequences &)
#+end_src

* .zprofile
:PROPERTIES:
:header-args+: :tangle ".zprofile"
:END:
** System-wide
Honor system-wide environment variables

#+begin_src sh
  source /etc/profile
#+end_src
** Guix profile loading
Test if on a Guix System to set the extra profiles variable and source them. Otherwise, set and source the default one on a foreign distribution. For Guix, also source anything in a profiles ~/etc/profile.d~ directory (while this an expected place, have only seen Flatpak put something in there on Guix).

#+begin_src sh
  if [[ -s /run/current-system/profile ]]; then
      export GUIX_EXTRA_PROFILES="$HOME/.config/guix/profiles"
      for i in $GUIX_EXTRA_PROFILES/*; do
          profile=$i/$(basename "$i")
          if [ -f "$profile"/etc/profile ]; then
              GUIX_PROFILE="$profile"
              . "$GUIX_PROFILE"/etc/profile
          fi

          if [ -d "$profile"/etc/profile.d/ ]; then
              for j in "$profile"/etc/profile.d/*.sh ; do
                  if [ -r "$j" ]; then
                      . $j
                  fi
              done
          fi

          unset profile
      done
  else
      export GUIX_PROFILE="$HOME/.config/guix/current"
      . "$GUIX_PROFILE/etc/profile"
      #  export GUIX_LOCPATH=$HOME/.guix-profile/lib/locale
  fi
#+end_src
** ~XDG_CONFIG_DIRs~ fix
To work around some search paths not being exported consistently in Guix, e.g. [[https://issues.guix.gnu.org/50103][issue #50103]], add the desktop profile to ~XDG_CONFIG_DIRS~

#+begin_src sh
  if [[ -s /run/current-system/profile ]]; then
      export XDG_CONFIG_DIRS=$GUIX_EXTRA_PROFILES/desktop/desktop/etc/xdg:$XDG_CONFIG_DIRS
  fi
#+end_src

** xdg-desktop-portal fix
Seems like a bug that the portals do not like multiple directories for ~XDG_DESKTOP_PORTAL_DIR~. So override it manually for now

#+begin_src sh :tangle no
  if [[ -s /run/current-system/profile ]]; then
      export XDG_DESKTOP_PORTAL_DIR=$GUIX_EXTRA_PROFILES/desktop/desktop/share/xdg-desktop-portal/portals
  fi
#+end_src

* .zshrc
:PROPERTIES:
:header-args+: :tangle ".zshrc"
:END:
** Plugins with [[https://zdharma.github.io/zinit/wiki/][Zinit]]
*** From the installer
Basic loading and initialization
#+begin_src sh :tangle no
  if [[ ! -f $HOME/.zinit/bin/zinit.zsh ]]; then
      print -P "%F{33}▓▒░ %F{220}Installing %F{33}DHARMA%F{220} Initiative Plugin Manager (%F{33}zdharma/zinit%F{220})…%f"
      command mkdir -p "$HOME/.zinit" && command chmod g-rwX "$HOME/.zinit"
      command git clone https://github.com/zdharma/zinit "$HOME/.zinit/bin" && \
          print -P "%F{33}▓▒░ %F{34}Installation successful.%f%b" || \
          print -P "%F{160}▓▒░ The clone has failed.%f%b"
  fi

  source "$HOME/.zinit/bin/zinit.zsh"
  autoload -Uz _zinit
  (( ${+_comps} )) && _comps[zinit]=_zinit
#+end_src

Load a few important annexes, without Turbo (this is currently required for annexes)
#+begin_src sh :tangle no
  zinit light-mode for \
      zinit-zsh/z-a-rust \
      zinit-zsh/z-a-as-monitor \
      zinit-zsh/z-a-patch-dl \
      zinit-zsh/z-a-bin-gem-node
#+end_src

** Plugins with [[https://z.digitalclouds.dev/][ZI]]
*** p10k instant prompt
#+begin_src sh
  # Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
  # Initialization code that may require console input (password prompts, [y/n]
  # confirmations, etc.) must go above this block; everything else may go below.
  if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
  fi
#+end_src
*** Basic loading from installer
#+begin_src sh
  if [[ -r "$HOME/.config/zi/init.zsh" ]]; then
    source "$HOME/.config/zi/init.zsh" && zzinit
  fi
  zi light-mode for \
    z-shell/z-a-meta-plugins \
    @annexes # <- https://z.digitalclouds.dev/ecosystem/annexes
  # examples here -> https://z.digitalclouds.dev/docs/gallery/collection
  zicompinit # <- https://z.digitalclouds.dev/docs/guides/commands
#+end_src
*** Prompt
Load powerlevel10k theme:
#+begin_src sh
  zinit light romkatv/powerlevel10k

  # To customize prompt, run `p10k configure` or edit ~/.p10k.zsh.
  [[ ! -f ~/.p10k.zsh ]] || source ~/.p10k.zsh
#+end_src
*** Misc
Color for man pages:
#+begin_src sh
  zi ice use:"*.zsh"
  zi light zuxfoucault/colored-man-pages_mod
#+end_src
** History and completion
#+begin_src sh
  HISTFILE=~/.histfile
  HISTSIZE=10000
  SAVEHIST=10000
  setopt hist_ignore_dups append_history extended_history autocd

  # The following lines were added by compinstall
  zstyle :compinstall filename '$HOME/.zshrc'

  autoload -Uz compinit
  compinit
  # End of lines added by compinstall

  # Completion for kitty
  kitty + complete setup zsh | source /dev/stdin

  setopt sharehistory
  setopt extendedhistory

  # superglobs
  setopt extendedglob
  unsetopt caseglob

  # correct commands
  setopt correct

  # Tab completion from both ends
  setopt completeinword
  # Case-insensitive
  zstyle ':completion:*' matcher-list 'm:{a-zA-Z}={A-Za-z}'
  # Better kilall completion
  zstyle ':completion:*:killall:*' command 'ps -u $USER -o cmd'
#+end_src

** Keybindings
Set emacs style (by default through ~EDITOR~, but anyway)
#+begin_src sh
  bindkey -e
#+end_src

Set up general keybindings, mostly just didn't have delete key registering correctly in Termite or Emacs
#+begin_src sh
  # create a zkbd compatible hash;
  # to add other keys to this hash, see: man 5 terminfo
  typeset -g -A key

  key[Home]="${terminfo[khome]}"
  key[End]="${terminfo[kend]}"
  key[Insert]="${terminfo[kich1]}"
  key[Backspace]="${terminfo[kbs]}"
  key[Delete]="${terminfo[kdch1]}"
  key[Up]="${terminfo[kcuu1]}"
  key[Down]="${terminfo[kcud1]}"
  key[Left]="${terminfo[kcub1]}"
  key[Right]="${terminfo[kcuf1]}"
  key[PageUp]="${terminfo[kpp]}"
  key[PageDown]="${terminfo[knp]}"
  key[Shift-Tab]="${terminfo[kcbt]}"

  # setup key accordingly
  [[ -n "${key[Home]}"      ]] && bindkey -- "${key[Home]}"      beginning-of-line
  [[ -n "${key[End]}"       ]] && bindkey -- "${key[End]}"       end-of-line
  [[ -n "${key[Insert]}"    ]] && bindkey -- "${key[Insert]}"    overwrite-mode
  [[ -n "${key[Backspace]}" ]] && bindkey -- "${key[Backspace]}" backward-delete-char
  [[ -n "${key[Delete]}"    ]] && bindkey -- "${key[Delete]}"    delete-char
  [[ -n "${key[Up]}"        ]] && bindkey -- "${key[Up]}"        up-line-or-history
  [[ -n "${key[Down]}"      ]] && bindkey -- "${key[Down]}"      down-line-or-history
  [[ -n "${key[Left]}"      ]] && bindkey -- "${key[Left]}"      backward-char
  [[ -n "${key[Right]}"     ]] && bindkey -- "${key[Right]}"     forward-char
  [[ -n "${key[PageUp]}"    ]] && bindkey -- "${key[PageUp]}"    beginning-of-buffer-or-history
  [[ -n "${key[PageDown]}"  ]] && bindkey -- "${key[PageDown]}"  end-of-buffer-or-history
  [[ -n "${key[Shift-Tab]}" ]] && bindkey -- "${key[Shift-Tab]}" reverse-menu-complete

  # Finally, make sure the terminal is in application mode, when zle is
  # active. Only then are the values from $terminfo valid.
  if (( ${+terminfo[smkx]} && ${+terminfo[rmkx]} )); then
          autoload -Uz add-zle-hook-widget
          function zle_application_mode_start { echoti smkx }
          function zle_application_mode_stop { echoti rmkx }
          add-zle-hook-widget -Uz zle-line-init zle_application_mode_start
          add-zle-hook-widget -Uz zle-line-finish zle_application_mode_stop
  fi
#+end_src

** Aliases
#+begin_src sh
  alias ls='ls --color=auto -F'
  alias ll='ls -lahF --color=auto'
  eval "$(thefuck --alias)"
  alias gp='grep --color -rniC 1'
  # Completion for aliases too
  setopt COMPLETE_ALIASES
#+end_src

** Looks
#+begin_src sh
  # Colors from wpgtk
  #(cat ~/.config/wpg/sequences &)

  #
  # Prompt stuff
  #
  setopt interactivecomments # pound sign in interactive prompt
  # Report CPU stats for long (> 10s) commands
  REPORTTIME=10

  # powerlevel9k prompt
  #POWERLEVEL9K_MODE='awesome-fontconfig'
  #POWERLEVEL9K_LEFT_PROMPT_ELEMENTS=(root_indicator context dir vcs)
  #POWERLEVEL9K_RIGHT_PROMPT_ELEMENTS=(status)
  #POWERLEVEL9K_COLOR_SCHEME='dark'
  #POWERLEVEL9K_PROMPT_ON_NEWLINE=true
  #POWERLEVEL9K_OK_ICON='✓'

  #
  # Window title
  #
  autoload -Uz add-zsh-hook

  function xterm_title_precmd () {
          print -Pn '\e]2;%n@%m %~\a'
          [[ "$TERM" == 'screen'* ]] && print -Pn '\e_\005{g}%n\005{-}@\005{m}%m\005{-} \005{B}%~\005{-}\e\\'
  }

  function xterm_title_preexec () {
          print -Pn '\e]2;%n@%m %~ %# ' && print -n "${(q)1}\a"
          [[ "$TERM" == 'screen'* ]] && { print -Pn '\e_\005{g}%n\005{-}@\005{m}%m\005{-} \005{B}%~\005{-} %# ' && print -n "${(q)1}\e\\"; }
  }

  if [[ "$TERM" == (screen*|xterm*|rxvt*|termite*|kitty*) ]]; then
          add-zsh-hook -Uz precmd xterm_title_precmd
          add-zsh-hook -Uz preexec xterm_title_preexec
  fi
#+end_src
